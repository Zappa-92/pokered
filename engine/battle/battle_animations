SECTION "Battle Animations", ROMX, BANK[$10]

HandleExplodingAnimation:
    ld a, [H_WHOSETURN]
    and a
    ld hl, wEnemyMonType1
    ld de, wEnemyBattleStatus1
    ld a, [wPlayerMoveNum]
    jr z, .player
    ld hl, wBattleMonType1
    ld de, wEnemyBattleStatus1
    ld a, [wEnemyMoveNum]
.player
    cp SELFDESTRUCT
    jr z, .isExplodingMove
    cp EXPLOSION
    ret nz
.isExplodingMove
    ld a, [de]
    bit INVULNERABLE, a
    ret nz
    ld a, [hli]
    cp GHOST
    ret z
    ld a, [hl]
    cp GHOST
    ret z
    ld a, [wMoveMissed]
    and a
    ret nz
    ld a, 5
    ld [wAnimationType], a
    ret

; show 2 stages of the player mon getting smaller before disappearing
AnimateRetreatingPlayerMon:
	coord hl, 1, 5
	lb bc, 7, 7
	call ClearScreenArea
	coord hl, 3, 7
	lb bc, 5, 5
	xor a
	ld [wDownscaledMonSize], a
	ld [hBaseTileID], a
	predef CopyDownscaledMonTiles
	ld c, 4
	call DelayFrames
	call .clearScreenArea
	coord hl, 4, 9
	lb bc, 3, 3
	ld a, 1
	ld [wDownscaledMonSize], a
	xor a
	ld [hBaseTileID], a
	predef CopyDownscaledMonTiles
	call Delay3
	call .clearScreenArea
	ld a, $4c
	Coorda 5, 11
.clearScreenArea
	coord hl, 1, 5
	lb bc, 7, 7
call ClearScreenArea
ret
